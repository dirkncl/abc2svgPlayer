%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 
% This file is a part of the Open Hymnal Project to create a freely 
% distributable, downloadable database of Christian hymns, spiritual 
% songs, and prelude/postlude music.  This music is to be distributed 
% as complete scores (words and music), using all accompaniment parts, 
% in formats that are easily accessible on most computer OS's and which
% can be freely modified by anyone.  The current format of choice is the 
% "ABC Plus" format, favored by folk music distributors on the internet.
% All scores will also be converted into pdf, MIDI, and mp3 formats.
% Some advanced features of ABC Plus are used, and for accurate 
% translation to a printed score, please consider using "abcm2ps" 
% version 4.10 or later.  I am doing my best to create a final product
% that is "Hymnal-quality", and could feasibly be used as the basis for
% a printed church hymnal.
%
% The maintainer of the Open Hymnal Project is Brian J. Dumont (brian 
% dot j dot dumont at gmail dot com).  I have gone through serious efforts 
% to make sure that no copyright mistakes make it into this database.
% If I am in error, please inform me as soon as possible.
%
% This entire effort has used only free software, and I am indebted to 
% the efforts of many other individuals, including the authors of
% the various ABC and ABC Plus software.  Undoubtedly, I am also indebted 
% to all of the great Christians who wrote these hymns.
%
% This database comes with no guarantees whatsoever.
%
% I would love to get email from anyone who uses the Open Hymnal, and
% I will take requests for hymns to add.  My decision of whether to 
% add a hymn will be based on these criteria (in the following order):
% 1) It must be in the public domain or freely distributable
% 2) It must be a Christian piece
% 3) Whether I have access to a printed copy of the music (surprisingly,
%    a MIDI file is usually a terrible source)
% 4) Whether I like the hymn :)
%
% If you would like to contribute to the Open Hymnal Project, please 
% send an email to me, I would love the help!  PLEASE EMAIL ME IF YOU 
% FIND ANY MISTAKES, no matter how small.  I want to ensure that every 
% slur, stem, hyphenation, and punctuation mark is correct; and I'm sure 
% that there must be mistakes right now.
%
% Open Hymnal Project, 2009 Edition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PAGE LAYOUT
%
%%pagewidth	21.6000cm
%%pageheight	27.9000cm
%%scale		0.7500000
%%staffsep	1.60000cm
%%exprabove	false
%%measurebox	false
%OHSCRIP 2Thess 3:16, Ps 35:23, Is 31:4-5
%OHTOPICS {Christian Life}, {Church on Earth (Church Militant)}, {Courage in Affliction}, {God, Majesty &amp; Power of}, {God, Mercy of}, {Hope}, {Humility}, {Invitation (Faithful's to God)}, {Opening Songs}, {Peace}, {Salvation}, {Security}, {Submission (see also Following Jesus, God - Providence of, Trust)}, {Trust}
%OHCATEGORY TRUST
%OHMETRICAL 8 7 8 7 8
%OHCOMPLEXITY 4
%OHCOMPOSER Luther, Martin (1483-1546)
%OHARRANGER Erythraeus
%OHAUTHOR Latin;Luther, Martin (1483-1546)
%OHTRANSLATOR Massie, Richard (1800-1887)
%%postscript /st { moveto -8 0 rmoveto 0 8 rlineto 4 0 rlineto stroke } def
%%postscript /nd { moveto -8 0 rmoveto 0 8 rlineto -4 0 rlineto stroke } def
%%deco sintro 6 st 6 0 0 
%%deco eintro 6 nd 6 0 0 

X: 1
T: In These Our Days So Perilous
T: (also known as Grant Peace, We Pray, in Mercy, Lord or Grant Peace in Mercy, Lord, We Pray)
C: Words: Latin c. 6th century, adapted by Martin Luther, 1529.  Translated to English by Richard Massie, 1854, alt. 
C: Music: 'Verleih uns Frieden Gnädiglich' Martin Luther.  Found in Klug's Geistliche Lieder, 1533. 
C: Setting: Erythraeus, 1608.
C: copyright: public domain.  This score is a part of the Open Hymnal Project, 2009 Revision.
M: 4/4 % time signature
L: 1/4 % default length
%%staves (S1V1 S1V2) | (S2V1 S2V2) 
V: S1V1 clef=treble 
%%MIDI beat 127 125 110 1
V: S1V2 
%%MIDI beat 127 125 110 1
V: S2V1 clef=bass 
%%MIDI beat 127 125 110 1
V: S2V2 
%%MIDI beat 127 125 110 1
K: F % key signature
%
%%MIDI program 1 0 % Piano 1
%%MIDI program 2 0 % Piano 1
%%MIDI program 3 0 % Piano 1
%%MIDI program 4 0 % Piano 1
%
% 1
[V: S1V1] [Q:1/4=110] G2 G G | F G B A | !fermata!G2 B2 | c c B c | d2 !fermata!d2 |
w: 1.~In these our days so per- il- ous, Lord, peace in mer- cy send us; 
[V: S1V2]  D2 D D | D E (F//D// G) ^F/ | !ferm!G2 G2 | A E G G | (G ^F/E/) !ferm!^F2 |
[V: S2V1]  B,2 B, B, | A, C D D | !invertedfermata!B,2 D2 | F C D G, | (B, A,/G,/) !invertedfermata!A,2 |
[V: S2V2]  G,2 G, B,, | D, C, B,,3/4C,//D,/ D,/ | !ferm!G,,2 G,2 | F, A, G, _E, | D,2 !ferm!D,2 |
% 5
[V: S1V1]  d d c d | B c (B A) | !fermata!G2 G =F | G (B c) G | (^F G) !fermata!G2 |
w: No God but Thee can fight for * us, No God but Thee * de- fend * us; 
[V: S1V2]  =F F3/2 F/ F | G G2 ^F | !ferm!G2 D D | D G3/4F//_E3/4D// C | D2 !ferm!D2 |
[V: S2V1]  A, B, A, A, | G, (_E D3/2) C/ | !invertedfermata!B,2 B, A, | (B,3/4A,// G,) G, G, | (A, B,) !invertedfermata!B,2 |
[V: S2V2]  D, B,, F, D, | E, C, D,2 | !ferm!G,,2 G, D, | G, _E,3/4D,//C,3/4D,// _E, | (D, G,,) !ferm!G,,2 |
% 9
[V: S1V1]  !sintro!c2 (d c) | B G B A/G/ | (B2 G2) | G4 !eintro!|]
w: Thou our * on- ly God and * Sa- * vior. 
[V: S1V2]  F3 A | G2 D _E | (D2 _E2) | D4 |]
[V: S2V1]  A,2 (B,/A,//G,// A,) | D3/2 C/ B, C | (G, G,/A,/ B, C) | =B,4 |]
[V: S2V2]  F,,2 (B,, F,,) | G,, G,2 C, | (G,3/2 F,/ _E,/D,/ C,) | G,,4 |]
% 15
