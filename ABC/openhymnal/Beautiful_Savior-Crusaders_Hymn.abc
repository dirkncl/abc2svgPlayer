%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 
% This file is a part of the Open Hymnal Project to create a freely 
% distributable, downloadable database of Christian hymns, spiritual 
% songs, and prelude/postlude music.  This music is to be distributed 
% as complete scores (words and music), using all accompaniment parts, 
% in formats that are easily accessible on most computer OS's and which
% can be freely modified by anyone.  The current format of choice is the 
% "ABC Plus" format, favored by folk music distributors on the internet.
% All scores will also be converted into pdf, MIDI, and mp3 formats.
% Some advanced features of ABC Plus are used, and for accurate 
% translation to a printed score, please consider using "abcm2ps" 
% version 4.10 or later.  I am doing my best to create a final product
% that is "Hymnal-quality", and could feasibly be used as the basis for
% a printed church hymnal.
%
% The maintainer of the Open Hymnal Project is Brian J. Dumont (brian 
% dot j dot dumont at gmail dot com).  I have gone through serious efforts 
% to make sure that no copyright mistakes make it into this database.
% If I am in error, please inform me as soon as possible.
%
% This entire effort has used only free software, and I am indebted to 
% the efforts of many other individuals, including the authors of
% the various ABC and ABC Plus software.  Undoubtedly, I am also indebted 
% to all of the great Christians who wrote these hymns.
%
% This database comes with no guarantees whatsoever.
%
% I would love to get email from anyone who uses the Open Hymnal, and
% I will take requests for hymns to add.  My decision of whether to 
% add a hymn will be based on these criteria (in the following order):
% 1) It must be in the public domain or freely distributable
% 2) It must be a Christian piece
% 3) Whether I have access to a printed copy of the music (surprisingly,
%    a MIDI file is usually a terrible source)
% 4) Whether I like the hymn :)
%
% If you would like to contribute to the Open Hymnal Project, please 
% send an email to me, I would love the help!  PLEASE EMAIL ME IF YOU 
% FIND ANY MISTAKES, no matter how small.  I want to ensure that every 
% slur, stem, hyphenation, and punctuation mark is correct; and I'm sure 
% that there must be mistakes right now.
%
% Open Hymnal Project, 2005 Edition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PAGE LAYOUT
%
%%pagewidth	21.6000cm
%%pageheight	27.9000cm
%%scale		0.750000
%%staffsep	1.60000cm
%%exprabove	false
%%measurebox	false
%OHSCRIP So 6:10, Mt 6:29
%OHTOPICS {Crown [1]}, {God, As King [1]}, {Jesus}, {Joy [1]}, {Light [1]}, {Nature [2,3]}, {Praise, Son}, {Seasons [2]}, {Worship}
%OHCATEGORY REDEEMER
%OHMETRICAL 5 5 7 5 5 8
%OHCOMPLEXITY 2
%OHCOMPOSER Silesian
%OHARRANGER Willis, Richard Storrs (1819-1900)
%OHAUTHOR German Jesuits
%OHTRANSLATOR Seiss, Joseph August (1823-1904)
%%postscript /st { moveto -8 0 rmoveto 0 8 rlineto 4 0 rlineto stroke } def
%%postscript /nd { moveto -8 0 rmoveto 0 8 rlineto -4 0 rlineto stroke } def
%%deco sintro 6 st 6 0 0 
%%deco eintro 6 nd 6 0 0 

X: 1
T: Beautiful Savior 
T: (also known as Fairest Lord Jesus)
C: Words: German Jesuits, published 1677. Translated by Joseph A. Seiss, 1873.  
C: Music: 'Crusader`s Hymn' Silesian folk song, 1842.  Setting: Richard S. Willis, 1850.
C: copyright: public domain.  This score is a part of the Open Hymnal Project, 2005 Revision.
S: Music source: 'Lutheran Worship' Hymnal, 1982 Hymn 507.
M: 4/4 % time signature
L: 1/4 % default length
%%staves (S1V1 S1V2) | (S2V1 S2V2) 
V: S1V1 clef=treble 
%%MIDI beat 127 125 110 1
V: S1V2 
%%MIDI beat 127 125 110 1
V: S2V1 clef=bass 
%%MIDI beat 127 125 110 1
V: S2V2 
%%MIDI beat 127 125 110 1
K: Eb % key signature
%
%%MIDI program 1 0 % Piano 1
%%MIDI program 2 0 % Piano 1
%%MIDI program 3 0 % Piano 1
%%MIDI program 4 0 % Piano 1
%
% 1
[V: S1V1] [Q:1/4=120] E2 E E | (F D) E2 | G2 G G | (A F) G2 | B2 .(e c) |
w: 1.~Beau- ti- ful Sa- * vior, King of Cre- a- * tion Son of * 
w: 2.~Fair are the mea- * dows, Fair are the wood- * lands, Robed in the 
w: 3.~Fair is the sun- * shine, Fair is the moon- * light, Bright the * 
w: 4.~Beau- ti- ful Sa- * vior, Lord of the na- * tions, Son of * 
[V: S1V2]  B,2 C C | (C B,) B,2 | E2 E =E | (F D) E2 | E2 E2 |
[V: S2V1]  G,2 G, G, | (A, F,) G,2 | B,2 C C | (C B,) B,2 | G,2 .(C A,) |
[V: S2V2]  E,2 C, C, | (A,, B,,) E,2 | (E, D,) C, B,, | (A,, B,,) E,2 | E,2 .(A,, C,) |
% 5
[V: S1V1]  B2 (A G) | A2 G2 | F4 | B2 c B | (B G) A2 |
w: God and * Son of Man! Tru- ly I'd love * Thee, 
w: flow'rs of * bloom- ing spring; Je- sus is fair- * er, 
w: spark- ling * stars on high; Je- sus shines bright- * er, 
w: God and * Son of Man! Glo- ry and ho- * nor, 
[V: S1V2]  E2 (F E) | (E D) E2 | D4 | E2 E E | =E2 F2 |
[V: S2V1]  G,2 B,2 | A,2 B,2 | B,4 | B,2 A, B, | C2 C2 |
[V: S2V2]  E,2 E,2 | F,2 E,2 | (B,,2 B, A,) | G,2 A, G, | C,2 F,2 |
% 9
[V: S1V1]  A2 B A | (A F) G2 | !sintro!G2 G G | B3 A | G2 F2 | E4 !eintro!|]
w: tru- ly I'd serve * Thee, Light of my soul, my joy, my crown. 
w: Je- sus is pur- * er, He makes our sor- r'wing spi- rit sing. 
w: Je- sus shines pur- * er Than all the an- gels in the sky. 
w: Praise, a- dor- a- * tion Now and for- ev- er- more be Thine! 
[V: S1V2]  F2 G F | (F D) E2 | E2 E E | E3 E | E2 D2 | B,4 |]
[V: S2V1]  D2 B, C | (D B,) B,2 | B,2 B, C | (B, C _D) C | B,2 (B, A,) | G,4 |]
[V: S2V2]  B,,2 B,, B,, | B,,2 E,2 | E,2 D, C, | G,,3 A,, | (B,, A,,) B,,2 | E,4 |]
% 18
