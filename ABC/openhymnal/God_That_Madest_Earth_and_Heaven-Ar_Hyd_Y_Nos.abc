%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 
% This file is a part of the Open Hymnal Project to create a freely 
% distributable, downloadable database of Christian hymns, spiritual 
% songs, and prelude/postlude music.  This music is to be distributed 
% as complete scores (words and music), using all accompaniment parts, 
% in formats that are easily accessible on most computer OS's and which
% can be freely modified by anyone.  The current format of choice is the 
% "ABC Plus" format, favored by folk music distributors on the internet.
% All scores will also be converted into pdf, MIDI, and mp3 formats.
% Some advanced features of ABC Plus are used, and for accurate 
% translation to a printed score, please consider using "abcm2ps" 
% version 4.10 or later.  I am doing my best to create a final product
% that is "Hymnal-quality", and could feasibly be used as the basis for
% a printed church hymnal.
%
% The maintainer of the Open Hymnal Project is Brian J. Dumont (brian 
% dot j dot dumont at gmail dot com).  I have gone through serious efforts 
% to make sure that no copyright mistakes make it into this database.
% If I am in error, please inform me as soon as possible.
%
% This entire effort has used only free software, and I am indebted to 
% the efforts of many other individuals, including the authors of
% the various ABC and ABC Plus software.  Undoubtedly, I am also indebted 
% to all of the great Christians who wrote these hymns.
%
% This database comes with no guarantees whatsoever.
%
% I would love to get email from anyone who uses the Open Hymnal, and
% I will take requests for hymns to add.  My decision of whether to 
% add a hymn will be based on these criteria (in the following order):
% 1) It must be in the public domain or freely distributable
% 2) It must be a Christian piece
% 3) Whether I have access to a printed copy of the music (surprisingly,
%    a MIDI file is usually a terrible source)
% 4) Whether I like the hymn :)
%
% If you would like to contribute to the Open Hymnal Project, please 
% send an email to me, I would love the help!  PLEASE EMAIL ME IF YOU 
% FIND ANY MISTAKES, no matter how small.  I want to ensure that every 
% slur, stem, hyphenation, and punctuation mark is correct; and I'm sure 
% that there must be mistakes right now.
%
% Open Hymnal Project, 2009 Edition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PAGE LAYOUT
%
%%pagewidth	21.6000cm
%%pageheight	27.9000cm
%%scale		0.750000
%%staffsep	1.60000cm
%%exprabove	false
%%measurebox	false
%OHSCRIP Gen 1:1, Ps 121:1-8, Mt 11:28, 2Tim 4:18, Ps 91:9-12
%OHTOPICS {Activity [1]}, {Angels [1]}, {Anticipation [3]}, {Calmness}, {Christian Life}, {Courage in Affliction [2,3]}, {Day [1]}, {Duty (see also Law) [1,2]}, {Eternal Life [3]}, {Evening [1]}, {God, Creator [1]}, {God, Faithfulness of [3]}, {Guidance [2]}, {Light [1]}, {Peace [3]}, {Presence [3]}, {Rest [1,3]}, {Submission (see also Following Jesus, God - Providence of, Trust) [2]}
%OHCATEGORY EVENING
%OHMETRICAL 8 4 8 4 8 8 8 4
%OHCOMPLEXITY 3
%OHCOMPOSER Welsh
%OHARRANGER unknown
%OHAUTHOR Heber, Reginald (1783-1826);Mercer, William (1811-1873);Whately, Richard (1787-1863)
%OHTRANSLATOR none
%%postscript /st { moveto -8 0 rmoveto 0 8 rlineto 4 0 rlineto stroke } def
%%postscript /nd { moveto -8 0 rmoveto 0 8 rlineto -4 0 rlineto stroke } def
%%deco sintro 6 st 6 0 0 
%%deco eintro 6 nd 6 0 0 

X: 1
T: God, That Madest Earth and Heaven
T: (also known as God, Who Madest Earth and Heaven or God, Who Made the Earth and Heaven)
C: Words: verse 1, Reginald Heber, 1827. verse 2, William Mercer, 1864. verse 3, Richard Whately, 1838. 
C: Music: 'Ar Hyd Y Nos' traditional Welsh.  Setting: "The English Hymnal", 1906.
C: copyright: public domain.  This score is a part of the Open Hymnal Project, 2009 Revision.
S: Music source: The English Hymnal, 1906 Hymn 268.
M: 4/4 % time signature
L: 1/4 % default length
%%staves (S1V1 S1V2) | (S2V1 S2V2) 
V: S1V1 clef=treble 
%%MIDI beat 127 125 110 1
V: S1V2 
%%MIDI beat 127 125 110 1
V: S2V1 clef=bass 
%%MIDI beat 127 125 110 1
V: S2V2 
%%MIDI beat 127 125 110 1
K: G % key signature
%
%%MIDI program 1 0 % Piano 1
%%MIDI program 2 0 % Piano 1
%%MIDI program 3 0 % Piano 1
%%MIDI program 4 0 % Piano 1
%
% 1
[V: S1V1] [Q:1/4=100] G3/2 F/ E G | A3/2 G/ F D | E2 F3/2 F/ | G4 |
w: 1.~God, that ma- dest earth and Hea- ven, dark- ness and light; 
w: 2.~And when morn a- gain shall call us, to run life's way, 
w: 3.~Guard us wa- king, guard us sleep- ing, and when we die, 
[V: S1V2]  B, D E D | E E D D | G,2 D C | B,4 |
[V: S2V1]  G, G, G, G, | C3/2 B,/ A, D | (D C) B, A, | G,4 |
[V: S2V2]  G,, B,, C, B,, | A,, C, D, B,, | C,2 D,3/2 D,/ | G,,4 |
% 5
[V: S1V1]  G3/2 F/ E G | A3/2 G/ F D | E2 F3/2 F/ | G4 |
w: Who the day for toil hast giv- en, for rest the night; 
w: May we still, what- e'er be- fall us, Thy will o- bey. 
w: May we in Thy might- y keep- ing all peace- ful lie; 
[V: S1V2]  B, D E D | E E D D | G,2 D C | B,4 |
[V: S2V1]  G, G, G, G, | C3/2 B,/ A, D | (D C) B, A, | G,4 |
[V: S2V2]  G,, B,, C, B,, | A,, C, D, B,, | C,2 D,3/2 D,/ | G,,4 |
% 9
[V: S1V1]  c B c d | e d c B | c B A G | B3/2 A/ G F |
w: May Thine an- gel guards de- fend us, Slum- ber sweet Thy mer- cy send us; 
w: From the pow'r of e- vil hide us, In the nar- row path- way guide us, 
w: When the last dread call shall wake us, Do not Thou, our God, for- sake us, 
[V: S1V2]  C D C G | G =F E D | E G E E | E3/2 E/ D D |
[V: S2V1]  G, G, G, G, | C A, G, B, | A, D C B, | C3/2 C/ A, A, |
[V: S2V2]  E, G, E, B,, | C, D, E, G, | A,, B,, C, E, | A,,3/2 C,/ D, D, |
% 13
[V: S1V1]  G3/2 F/ E G | A3/2 G/ F D | !sintro!E2 F3/2 F/ | G4 !eintro!|]
w: Ho- ly dreams and hopes at- tend us, all through the night. 
w: Nor Thy smile be e'er de- nied us all through the day. 
w: But to reign in glo- ry take us with Thee on high. 
[V: S1V2]  D3/2 D/ C D | E E D D | G,2 D C | B,4 |]
[V: S2V1]  G,3/2 G,/ G, G, | C3/2 B,/ A, D | (D C) B, A, | G,4 |]
[V: S2V2]  B,,3/2 B,,/ C, B,, | A,, C, D, B,, | C,2 D,3/2 D,/ | G,,4 |]
% 18
