%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 
% This file is a part of the Open Hymnal Project to create a freely 
% distributable, downloadable database of Christian hymns, spiritual 
% songs, and prelude/postlude music.  This music is to be distributed 
% as complete scores (words and music), using all accompaniment parts, 
% in formats that are easily accessible on most computer OS's and which
% can be freely modified by anyone.  The current format of choice is the 
% "ABC Plus" format, favored by folk music distributors on the internet.
% All scores will also be converted into pdf, MIDI, and mp3 formats.
% Some advanced features of ABC Plus are used, and for accurate 
% translation to a printed score, please consider using "abcm2ps" 
% version 4.10 or later.  I am doing my best to create a final product
% that is "Hymnal-quality", and could feasibly be used as the basis for
% a printed church hymnal.
%
% The maintainer of the Open Hymnal Project is Brian J. Dumont (brian 
% dot j dot dumont at gmail dot com).  I have gone through serious efforts 
% to make sure that no copyright mistakes make it into this database.
% If I am in error, please inform me as soon as possible.
%
% This entire effort has used only free software, and I am indebted to 
% the efforts of many other individuals, including the authors of
% the various ABC and ABC Plus software.  Undoubtedly, I am also indebted 
% to all of the great Christians who wrote these hymns.
%
% This database comes with no guarantees whatsoever.
%
% I would love to get email from anyone who uses the Open Hymnal, and
% I will take requests for hymns to add.  My decision of whether to 
% add a hymn will be based on these criteria (in the following order):
% 1) It must be in the public domain or freely distributable
% 2) It must be a Christian piece
% 3) Whether I have access to a printed copy of the music (surprisingly,
%    a MIDI file is usually a terrible source)
% 4) Whether I like the hymn :)
%
% If you would like to contribute to the Open Hymnal Project, please 
% send an email to me, I would love the help!  PLEASE EMAIL ME IF YOU 
% FIND ANY MISTAKES, no matter how small.  I want to ensure that every 
% slur, stem, hyphenation, and punctuation mark is correct; and I'm sure 
% that there must be mistakes right now.
%
% Open Hymnal Project, 2011 Edition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PAGE LAYOUT
%
%%pagewidth	21.6000cm
%%pageheight	27.9000cm
%%scale		0.750000
%%staffsep	1.600000cm
%%sysstaffsep	0.00000cm
%%exprabove	false
%%measurebox	false
%OHSCRIP Jn 17:11, Eph 4:1-7, 4:11-16, 1Cor 10:16-17, 1:13, Ps 133, 2Tim 4:3-4, Jn 10:11
%OHTOPICS {Bread (of Life/Daily Bread)}, {Brotherhood}, {Church on Earth (Church Militant)}, {Faithfulness}, {Fellowship}, {God, Providence of}, {Guidance}, {Lord's Supper}, {Obedience}, {Patience}, {Saints (all Faithful)}, {Submission (see also Following Jesus, God - Providence of, Trust)}, {Trust}
%OHCATEGORY LORD'S SUPPER
%OHMETRICAL 10 10 10 10 10 10
%OHCOMPLEXITY 1
%OHCOMPOSER Monk, William Henry (1823-1889)
%OHARRANGER unknown
%OHAUTHOR Turton, William H. (1856-1938)
%OHTRANSLATOR none
%%postscript /st { moveto -8 0 rmoveto 0 8 rlineto 4 0 rlineto stroke } def
%%postscript /nd { moveto -8 0 rmoveto 0 8 rlineto -4 0 rlineto stroke } def
%%deco sintro 6 st 6 0 0 
%%deco eintro 6 nd 6 0 0 

X: 1
T: Thou, Who at Thy First Eucharist
T: (also known as Lord, Who the Night You Were Betrayed or At That First Eucharist)
C: Words: William H. Turton, 1881. 
C: Music: 'Unde Et Memores' William Henry Monk, 1875. 
C: Setting: "The Church Hymnal, Revised and Enlarged" (Episcopal), 1896.
C: copyright: public domain.  This score is a part of the Open Hymnal Project, 2011 Revision.
S: Music source: "The Church Hymnal, Revised and Enlarged" (Episcopal), 1896  Ed. Charles Hutchins Hymn 228 second tune. 
M: 4/4 % time signature
L: 1/4 % default length
%%staves (S1V1 S1V2) | (S2V1 S2V2) 
V: S1V1 clef=treble 
%%MIDI beat 127 125 110 1
V: S1V2 
%%MIDI beat 127 125 110 1
V: S2V1 clef=bass 
%%MIDI beat 127 125 110 1
V: S2V2 
%%MIDI beat 127 125 110 1
K: D % key signature
%
%%MIDI program 1 0 % Piano 1
%%MIDI program 2 0 % Piano 1
%%MIDI program 3 0 % Piano 1
%%MIDI program 4 0 % Piano 1
%
% 1
[V: S1V1] [Q:1/4=120] F | F3/2 D/ D E | F3/2 F/ F G | A3 F | E F G3/2 G/ |
w: 1.~Thou, who at Thy first Eu- cha- rist didst pray That all Thy Church might 
w: 2.~For all Thy Church, O Lord, we in- ter- cede; Make Thou our sad di- 
w: 3.~We pray Thee too for wan- d'rers from Thy fold; O bring them back, good 
w: 4.~So, Lord, at length when sac- ra- ments shall cease, May we be one with 
[V: S1V2] D | D3/2 D/ D D | D3/2 D/ D C | D3 D | C D E3/2 E/ |
[V: S2V1] A, | A,3/2 F,/ F, G, | A,3/2 A,/ A, A, | A,3 A, | A, A, B,3/2 B,/ |
[V: S2V2] D, | D,3/2 D,/ D, D, | D,3/2 D,/ D, E, | F,3 D, | G, F, E,3/2 D,/ |
% 6
[V: S1V1] G F A F | E3 F | F3/2 D/ D E | F3/2 F/ F G | A3
w: be for- ev- er one, Grant us at ev- 'ry Eu- cha- rist to say 
w: vi- sions soon to cease; Draw us the near- er each to each, we plead,
w: Shep- herd of the sheep, Back to the faith which saints be- lieved of old,
w: all Thy Church a- bove, One with Thy saints in one un- bro- ken peace,
[V: S1V2] E D D D | C3 D | D3/2 D/ D D | D3/2 D/ D D | D3
[V: S2V1] A, A, A, A, | A,3 A, | A,3/2 F,/ F, G, | A,3/2 D,/ D, E, | F,3
[V: S2V2] C, D, F,,3/2 G,,/ | A,,3 D, | D,3/2 D,/ D, D, | D,3/2 B,,/ B,, B,, | F,,3 
% 10
[V: S1V1] F | B A ^G3/2 ^G/ | A B c B | A3 A | d A G F |
w: With long- ing heart and soul, "Thy will be done." O may we all one 
w: By draw- ing all to Thee, O Prince of Peace; Thus may we all one 
w: Back to the Church which still that faith doth keep; Soon may we all one 
w: One with Thy saints in one un- bound- ed love; More bless- èd still, in 
[V: S1V2] D | D D D3/2 D/ | C F E D | C3 C | D D C D |
[V: S2V1] A, | G, A, B,3/2 B,/ | A, A, A, ^G, | A,3 A, | A, A, A, A, |
[V: S2V2] D, | G, F, E,3/2 E,/ | F,  D, E, E, | A,,3 G, | F, F, E, D, |
% 15
[V: S1V1] B B d B | A3 !sintro!A | G F E D | B3/2 G/ F E | D3 !eintro!|]
w: bread, one bo- dy be, Through this blest sac- ra- ment of un- i- ty.
w: bread, one bo- dy be, Through this blest sac- ra- ment of un- i- ty.
w: bread, one bo- dy be, Through this blest sac- ra- ment of un- i- ty.
w: peace and love to be One with the Trin- i- ty in un- i- ty.
[V: S1V2] D D D G | F3 D | C D C D | D3/2 E/ D C | D3 |]
[V: S2V1] G, G, G, D | D3 A, | A, A, G, F, | G,3/2 B,/ A, G, | F,3 |]
[V: S2V2] G,, G,, B,, G,, | D,3 F, | E, D, A,, B,, | G,,3/2 G,,/ A,, A,, | D,3 |]



