%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 
% This file is a part of the Open Hymnal Project to create a freely 
% distributable, downloadable database of Christian hymns, spiritual 
% songs, and prelude/postlude music.  This music is to be distributed 
% as complete scores (words and music), using all accompaniment parts, 
% in formats that are easily accessible on most computer OS's and which
% can be freely modified by anyone.  The current format of choice is the 
% "ABC Plus" format, favored by folk music distributors on the internet.
% All scores will also be converted into pdf, MIDI, and mp3 formats.
% Some advanced features of ABC Plus are used, and for accurate 
% translation to a printed score, please consider using "abcm2ps" 
% version 4.10 or later.  I am doing my best to create a final product
% that is "Hymnal-quality", and could feasibly be used as the basis for
% a printed church hymnal.
%
% The maintainer of the Open Hymnal Project is Brian J. Dumont (brian 
% dot j dot dumont at gmail dot com).  I have gone through serious efforts 
% to make sure that no copyright mistakes make it into this database.
% If I am in error, please inform me as soon as possible.
%
% This entire effort has used only free software, and I am indebted to 
% the efforts of many other individuals, including the authors of
% the various ABC and ABC Plus software.  Undoubtedly, I am also indebted 
% to all of the great Christians who wrote these hymns.
%
% This database comes with no guarantees whatsoever.
%
% I would love to get email from anyone who uses the Open Hymnal, and
% I will take requests for hymns to add.  My decision of whether to 
% add a hymn will be based on these criteria (in the following order):
% 1) It must be in the public domain or freely distributable
% 2) It must be a Christian piece
% 3) Whether I have access to a printed copy of the music (surprisingly,
%    a MIDI file is usually a terrible source)
% 4) Whether I like the hymn :)
%
% If you would like to contribute to the Open Hymnal Project, please 
% send an email to me, I would love the help!  PLEASE EMAIL ME IF YOU 
% FIND ANY MISTAKES, no matter how small.  I want to ensure that every 
% slur, stem, hyphenation, and punctuation mark is correct; and I'm sure 
% that there must be mistakes right now.
%
% Open Hymnal Project, 2010 Edition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PAGE LAYOUT
%
%%pagewidth	21.6000cm
%%pageheight	27.9000cm
%%scale		0.7500000
%%staffsep	1.6000cm
%%exprabove	false
%%measurebox	false
%OHSCRIP Ps 89:26, Deut 32:1-4, Hab 1:12, Jn 19:34, 1Cor 10:4, Heb 9:13-14
%OHTOPICS {Atonement}, {Baptism [3]}, {Blood [1]}, {Brevity of Life [4]}, {Conversion [3]}, {Cross [3]}, {Faith}, {God, As King [4]}, {God, Majesty &amp; Power of}, {God, Mercy of}, {Grace}, {Holiness [1]}, {Jesus}, {Judgment [4]}, {Kindness, God's}, {Law [2]}, {Presence}, {Salvation}, {Sinners}, {Throne (also see God - As King) [4]}, {Trust}, {Zeal [2]}
%OHCATEGORY JUSTIFICATION
%OHMETRICAL 7 7 7 7 7 7
%OHCOMPLEXITY 1
%OHCOMPOSER Hastings, Thomas (1784-1872)
%OHARRANGER unknown
%OHAUTHOR Toplady, Augustus Montague (1740-1778)
%OHTRANSLATOR none
%%postscript /st { moveto -8 0 rmoveto 0 8 rlineto 4 0 rlineto stroke } def
%%postscript /nd { moveto -8 0 rmoveto 0 8 rlineto -4 0 rlineto stroke } def
%%deco sintro 6 st 6 0 0 
%%deco eintro 6 nd 6 0 0 

X: 1
T: Rock of Ages
C: Words: Augustus Montague Toplady, 1776.  Music: 'Toplady' Thomas Hastings, 1830. 
C: Setting: "Congregational Church Music with 150 Psalms and Hymns", 1854.
C: copyright: public domain.  This score is a part of the Open Hymnal Project, 2010 Revision.
S: Music source: "Congregational Church Music with 150 Psalms and Hymns", 1854 Hymn 188 p. 77.  Note this differs somewhat from Hasting's arrangement in "Spiritual Songs for Social Worship", 1837.
M: 6/4 % time signature
L: 1/4 % default length
%%staves (S1V1 S1V2) | (S2V1 S2V2) 
V: S1V1 clef=treble 
%%MIDI beat 127 125 110 1
V: S1V2 
%%MIDI beat 127 125 110 1
V: S2V1 clef=bass 
%%MIDI beat 127 125 110 1
V: S2V2 
%%MIDI beat 127 125 110 1
K: Bb % key signature
%
%%MIDI program 1 0 % Piano 1
%%MIDI program 2 0 % Piano 1
%%MIDI program 3 0 % Piano 1
%%MIDI program 4 0 % Piano 1
%
% 1
[V: S1V1] [Q:1/4=170] F3/2 G/ | F2 D2 B3/2 G/ | F4 B3/2 c/ | d3 c B A | B4
w: 1.~Rock of A- ges, cleft for me, Let me hide my- self in Thee; 
w: 2.~Not the la- bor of my hands Can ful- fill Thy law's de- mands; 
w: 3.~No- thing in my hand I bring, Simp- ly to the cross I cling; 
w: 4.~While I draw this fleet- ing breath, When mine eyes shall close in death, 
[V: S1V2]  D3/2 E/ | D2 B,2 D3/2 E/ | D4 F3/2 G/ | F3 E D C | D4
[V: S2V1]  B,3/2 B,/ | B,2 F,2 F,3/2 B,/ | B,4 B,3/2 B,/ | B,3 F, F, F, | F,4
[V: S2V2]  B,,3/2 B,,/ | B,,2 B,,2 B,,3/2 B,,/ | B,,4 D,3/2 E,/ | F,3 F, F,, F,, | B,,4
% 6
[V: S1V1]  A3/2 B/ | c3 c A F | B4 A3/2 B/ | c3 c A F | B4
w: Let the wa- ter and the blood, From Thy wound- ed side which flowed, 
w: Could my zeal no res- pite know, Could my tears for- ev- er flow, 
w: Na- ked, come to Thee for dress; Help- less look to Thee for grace; 
w: When I soar to worlds un- known, See Thee on Thy judg- ment throne, 
[V: S1V2]  C3/2 D/ | E3 E E E | D4 C3/2 D/ | E3 E E E | D4
[V: S2V1]  F,3/2 F,/ | F,3 F, F, F, | F,4 F,3/2 F,/ | F,3 F, F, F, | [F,4B,4]
[V: S2V2]  F,3/2 F,/ | F,3 F, F,, F,, | B,,4 F,3/2 F,/ | F,3 F, F,, F,, | B,,4
% 9
[V: S1V1]  F3/2 G/ | F2 D2 B3/2 G/ | F4 !sintro!B3/2 c/ | d3 c B A | B4 !eintro!|]
w: Be of sin the doub- le cure; Save from wrath and make me pure.  
w: All for sin could not a- tone; Thou must save, and Thou a- lone. 
w: Foul, I to the foun- tain fly; Wash me, Sa- vior, or I die. 
w: Rock of A- ges, cleft for me, Let me hide my- self in Thee. 
[V: S1V2]  D3/2 E/ | D2 B,2 D3/2 E/ | D4 F3/2 G/ | F3 E D C | D4 |]
[V: S2V1]  B,3/2 B,/ | B,2 F,2 F,3/2 B,/ | B,4 B,3/2 B,/ | B,3 F, F, F, | F,4 |]
[V: S2V2]  B,,3/2 B,,/ | B,,2 B,,2 B,,3/2 B,,/ | B,,4 D,3/2 E,/ | F,3 F, F,, F,, | B,,4 |]
% 15
