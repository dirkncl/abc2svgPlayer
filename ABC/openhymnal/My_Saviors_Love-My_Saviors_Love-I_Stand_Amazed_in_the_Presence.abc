%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 
% This file is a part of the Open Hymnal Project to create a freely 
% distributable, downloadable database of Christian hymns, spiritual 
% songs, and prelude/postlude music.  This music is to be distributed 
% as complete scores (words and music), using all accompaniment parts, 
% in formats that are easily accessible on most computer OS's and which
% can be freely modified by anyone.  The current format of choice is the 
% "ABC Plus" format, favored by folk music distributors on the internet.
% All scores will also be converted into pdf, MIDI, and mp3 formats.
% Some advanced features of ABC Plus are used, and for accurate 
% translation to a printed score, please consider using "abcm2ps" 
% version 4.10 or later.  I am doing my best to create a final product
% that is "Hymnal-quality", and could feasibly be used as the basis for
% a printed church hymnal.
%
% The maintainer of the Open Hymnal Project is Brian J. Dumont (brian 
% dot j dot dumont at gmail dot com).  I have gone through serious efforts 
% to make sure that no copyright mistakes make it into this database.
% If I am in error, please inform me as soon as possible.
%
% This entire effort has used only free software, and I am indebted to 
% the efforts of many other individuals, including the authors of
% the various ABC and ABC Plus software.  Undoubtedly, I am also indebted 
% to all of the great Christians who wrote these hymns.
%
% This database comes with no guarantees whatsoever.
%
% I would love to get email from anyone who uses the Open Hymnal, and
% I will take requests for hymns to add.  My decision of whether to 
% add a hymn will be based on these criteria (in the following order):
% 1) It must be in the public domain or freely distributable
% 2) It must be a Christian piece
% 3) Whether I have access to a printed copy of the music (surprisingly,
%    a MIDI file is usually a terrible source)
% 4) Whether I like the hymn :)
%
% If you would like to contribute to the Open Hymnal Project, please 
% send an email to me, I would love the help!  PLEASE EMAIL ME IF YOU 
% FIND ANY MISTAKES, no matter how small.  I want to ensure that every 
% slur, stem, hyphenation, and punctuation mark is correct; and I'm sure 
% that there must be mistakes right now.
%
% Open Hymnal Project, 2014 Edition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PAGE LAYOUT
%
%%pagewidth	21.6000cm
%%pageheight	27.9000cm
%%scale		0.750000
%%staffsep	1.600000cm
%%sysstaffsep	0.00000cm
%%exprabove	false
%%measurebox	false
%OHSCRIP 1Jn 4:9-10, Lk 22:41-44, Eph 3:14-19, 2:4-7, Rom 8:38-39
%OHTOPICS {Anticipation [4]}, {Atonement}, {Blood [2]}, {Compassion}, {Eternal Life [4]}, {God, Love of}, {Gratitude}, {Jesus}, {Love}, {Praise, Son}, {Presence}, {Sinners [1]}
%OHCATEGORY GOOD THURSDAY
%OHMETRICAL 8 7 8 7 4 4 7 4 4 7
%OHCOMPLEXITY 2
%OHCOMPOSER Gabriel, Charles Hutchinson (1856-1932)
%OHARRANGER Gabriel, Charles Hutchinson (1856-1932)
%OHAUTHOR Gabriel, Charles Hutchinson (1856-1932)
%OHTRANSLATOR none
%%postscript /st { moveto -8 0 rmoveto 0 8 rlineto 4 0 rlineto stroke } def
%%postscript /nd { moveto -8 0 rmoveto 0 8 rlineto -4 0 rlineto stroke } def
%%deco sintro 6 st 6 0 0 
%%deco eintro 6 nd 6 0 0 

X: 1
T: My Savior's Love
T: (also known as I Stand Amazed in the Presence)
C: Words: Charles H. Gabriel, 1905. 
C: Music and Setting: 'My Savior`s Love' or 'I Stand Amazed in the Presence' Charles H. Gabriel, 1905.
C: copyright: public domain.  This score is a part of the Open Hymnal Project, 2014 Revision.
S: Music source: Alexander's Gospel Songs #1, 1908 Hymn 1.  ABC file contributed to the Open Hymnal by Tobin Strong, 10 Feb 2014.
M: 4/4 % time signature
L: 1/4 % default length
%%staves (S1V1 S1V2) | (S2V1 S2V2) 
V: S1V1 clef=treble 
%%MIDI beat 127 125 110 1
V: S1V2 
%%MIDI beat 127 125 110 1
V: S2V1 clef=bass 
%%MIDI beat 127 125 110 1
V: S2V2 
%%MIDI beat 127 125 110 1
K: Ab % key signature
%
%%MIDI program 1 0 % Piano 1
%%MIDI program 2 0 % Piano 1
%%MIDI program 3 0 % Piano 1
%%MIDI program 4 0 % Piano 1
%
% 1
[V: S1V1] [Q:1/4=100] E | E C  E A3/4 B/4 | c2 A A | G G3/4 G/4 A B | A3
w: 1.~I stand a- mazed in the pres- ence Of Je- sus the Naz- a- rene, 
w: 2.~For me it was in the gar- den He pray'd "Not my will, but Thine;" 
w: 3.~In pit- y an- gels be- held Him, And came from the world of light 
w: 4.~When with the ran- somed in glo- ry His face I at last shall see, 
[V: S1V2] C | C A, C C3/4 D/4 | E2 C E | E E3/4 E/4 E E | E3
[V: S2V1] A,  | A,  E,  A,  A,3/4  A,/4  | A,2  A,  A, | B, B,3/4 B,/4 C  D  | C3
[V: S2V2] A,, | A,, A,, A,, A,,3/4 A,,/4 | A,,2 A,, C, | E, E,3/4 E,/4 E, E, | A,,3
% 5
[V: S1V1] E | F F  A G3/4 F/4 | E2 A B | c c3/4 c/4 B B | A3 z |
w: And won- der how He could love me, A sin- ner, con- demn'd, un- clean. 
w: He had no tears for His own griefs, But sweat drops of blood for mine. 
w: To com- fort Him in the sor- rows, He bore for my soul that night. 
w: 'Twill be my joy thro' the a- ges, To sing of His love for me. 
[V: S1V2] C | D D  F E3/4 D/4 | C2 E F | E E3/4 E/4 D D | C3 z |
[V: S2V1] A,  | A, A, A, A,3/4 A,/4 | A,2  A, A, | A, A,3/4 A,/4 G, G, | A,3  z |
[V: S2V2] A,, | D, D, D, D,3/4 D,/4 | A,,2 C, D, | E, E,3/4 E,/4 E, E, | A,,3 z |
% 9
[V: S1V1]  c2  c3/4 B/4 A | e2  e3/4 d/4 c | B B B3/2 c/ | d c B2 |
w: How mar- vel- ous! how won- der- ful! And my song shall ev- er be: 
[V: S1V2]  E E E3/4 D/4 C | E E E3/4 E/4 E | E E E3/2 E/ | E E E2 |
[V: S2V1]  A,  A,  A,3/4  A,/4  A,  | C  C  C3/4  B,/4 A, | G, G, G,3/2 A,/ | B, A, G,2 |
[V: S2V2]  A,, A,, A,,3/4 A,,/4 A,, | A, A, A,3/4 A,/4 A, | E, E, E,3/2 E,/ | E, E, E,2 |
w: O how mar- vel- ous! O how won- der- ful! And my song shall ev- er be: 
% 13
%%vskip 1.0cm
[V: S1V1]  !sintro!c2  c3/4 B/4 A | e2  e3/4 d/4 c | B c/d/ c3/2 B/ | A G !fermata!A !eintro!|]
w: How mar- vel- ous! how won- der- ful! Is my * Sav- ior's love for me! 
[V: S1V2]  E E E3/4 D/4 C | E E E3/4 E/4 E | F F    E3/2 E/ | E D !ferm!C |]
[V: S2V1]  A,  A,  A,3/4  A,/4  A,  | C  C  C3/4  B,/4 A, | A, A, A,3/2 D/  | C  B, !invertedfermata!A,  |]
[V: S2V2]  A,, A,, A,,3/4 A,,/4 A,, | A, A, A,3/4 A,/4 A, | D, D, E,3/2 E,/ | E, E, !ferm!A,, |]
w: O how mar- vel- ous! O how won- der- ful! Is my Sav- ior's love for me! 
