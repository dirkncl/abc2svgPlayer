%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 
% This file is a part of the Open Hymnal Project to create a freely 
% distributable, downloadable database of Christian hymns, spiritual 
% songs, and prelude/postlude music.  This music is to be distributed 
% as complete scores (words and music), using all accompaniment parts, 
% in formats that are easily accessible on most computer OS's and which
% can be freely modified by anyone.  The current format of choice is the 
% "ABC Plus" format, favored by folk music distributors on the internet.
% All scores will also be converted into pdf, MIDI, and mp3 formats.
% Some advanced features of ABC Plus are used, and for accurate 
% translation to a printed score, please consider using "abcm2ps" 
% version 4.10 or later.  I am doing my best to create a final product
% that is "Hymnal-quality", and could feasibly be used as the basis for
% a printed church hymnal.
%
% The maintainer of the Open Hymnal Project is Brian J. Dumont (brian 
% dot j dot dumont at gmail dot com).  I have gone through serious efforts 
% to make sure that no copyright mistakes make it into this database.
% If I am in error, please inform me as soon as possible.
%
% This entire effort has used only free software, and I am indebted to 
% the efforts of many other individuals, including the authors of
% the various ABC and ABC Plus software.  Undoubtedly, I am also indebted 
% to all of the great Christians who wrote these hymns.
%
% This database comes with no guarantees whatsoever.
%
% I would love to get email from anyone who uses the Open Hymnal, and
% I will take requests for hymns to add.  My decision of whether to 
% add a hymn will be based on these criteria (in the following order):
% 1) It must be in the public domain or freely distributable
% 2) It must be a Christian piece
% 3) Whether I have access to a printed copy of the music (surprisingly,
%    a MIDI file is usually a terrible source)
% 4) Whether I like the hymn :)
%
% If you would like to contribute to the Open Hymnal Project, please 
% send an email to me, I would love the help!  PLEASE EMAIL ME IF YOU 
% FIND ANY MISTAKES, no matter how small.  I want to ensure that every 
% slur, stem, hyphenation, and punctuation mark is correct; and I'm sure 
% that there must be mistakes right now.
%
% Open Hymnal Project, 2013 Edition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PAGE LAYOUT
%
%%pagewidth	21.6000cm
%%pageheight	27.9000cm
%%scale		0.750000
%%staffsep	1.600000cm
%%sysstaffsep	0.00000cm
%%exprabove	false
%%measurebox	false
%OHSCRIP 1Jn 4:13-16, Mt 28:20, Jn 1:1-14, Phil 4:13, Col 1:11-12
%OHTOPICS {Closing Songs}, {Darkness, Spiritual [3]}, {Following Jesus, Sanctification}, {God, Care of}, {Grace [1]}, {Invitation (Faithful's to God)}, {Jesus}, {Joy [2]}, {Light [3]}, {Love [6]}, {Peace [2]}, {Presence}, {Security [5]}, {Wisdom [4]}
%OHCATEGORY CLOSING SONGS
%OHMETRICAL 7 6 7 6
%OHCOMPLEXITY 1
%OHCOMPOSER Vulpius, Melchior (1560-1615)
%OHARRANGER unknown
%OHAUTHOR Stegmann, Joshua (1588-1632)
%OHTRANSLATOR Crull, August (1845-1923)
%%postscript /st { moveto -8 0 rmoveto 0 8 rlineto 4 0 rlineto stroke } def
%%postscript /nd { moveto -8 0 rmoveto 0 8 rlineto -4 0 rlineto stroke } def
%%deco sintro 6 st 6 0 0 
%%deco eintro 6 nd 6 0 0 

X: 1
T: Abide, O Dearest Jesus
T: (also known as Abide with Us, Lord Jesus or Abide Among Us with Thy Grace or 
T: Abide with Us, Our Savior or Come, Abide with Thy Grace, in Our Hearts or O Savior, Go Beside us)
C: Words: Joshua Stegmann, 1628.  Translated by August Crull circa 1892.
C: Music: 'Christus der ist mein Leben' Melchior Vulpius, 1609. 
C: Setting: "Mehrstimmiges ChoralBuch", 1906.
C: copyright: public domain.  This score is a part of the Open Hymnal Project, 2013 Revision.
S: Music source: "Mehrstimmiges ChoralBuch", 1906 Hymn #32, page 23 Ed. Karl Brauer.
M: 4/4 % time signature
L: 1/4 % default length
%%staves (S1V1 S1V2) | (S2V1 S2V2) 
V: S1V1 clef=treble 
%%MIDI beat 127 125 110 1
V: S1V2 
%%MIDI beat 127 125 110 1
V: S2V1 clef=bass 
%%MIDI beat 127 125 110 1
V: S2V2 
%%MIDI beat 127 125 110 1
K: Eb % key signature
%
%%MIDI program 1 0 % Piano 1
%%MIDI program 2 0 % Piano 1
%%MIDI program 3 0 % Piano 1
%%MIDI program 4 0 % Piano 1
%
% 1
[V: S1V1] [Q:1/4=120] E | G F G A | B2 !fermata!G c | B3/2 A/ G F | G2
w: 1.~A- bide, O dear- est Je- sus, A- mong us with Thy grace, 
w: 2.~A- bide, O dear Re- deem- er, A- mong us with Thy Word, 
w: 3.~A- bide with heav'n- ly bright- ness A- mong us, pre- cious Light; 
w: 4.~A- bide with rich- est bless- ings A- mong us, boun- teous Lord; 
w: 5.~A- bide with Thy pro- tec- tion A- mong us, Lord, our Strength, 
[V: S1V2] B, | E D E E | D2 !ferm!E E | E3/2 C/ E D | E2
[V: S2V1] G, | B, B, B, E, | (F, B,) !invertedfermata!B, A, | G,3/2 A,/ B, B, | B,2
[V: S2V2] E, | E, B,, E, C, | B,,2 !ferm!E, A,, | E,3/2 F,/ E, B,, | E,2
% 5
[V: S1V1]  z B | c c d e | c2 !fermata!B !sintro!G | F E E D | E3 !eintro!|]
w: That Sa- tan may not harm us, Nor we to sin give place. 
w: And thus now and here- af- ter True peace and joy af- ford. 
w: Thy truth di- rect, and keep us From er- ror's gloom- y night. 
w: Let us in grace and wis- dom Grow dai- ly through Thy Word. 
w: Lest world and Sa- tan fell us And o- ver- come at length. 
[V: S1V2]  x E | E E F E | E2 !ferm!E E | C B, C B, | B,3 |]
[V: S2V1]  z G, | A, G, B, B, | A,2 !invertedfermata!G, B, | A, G, F, F, | G,3 |]
[V: S2V2]  x E, | A,, C, B,, G,, | A,,2 !ferm!E, E,, | F,, G,, A,, B,, | E,,3 |]
W: 6. Abide, O faithful Savior, Among us with Thy love; 
W: Grant steadfastness, and help us To reach our home above. 
