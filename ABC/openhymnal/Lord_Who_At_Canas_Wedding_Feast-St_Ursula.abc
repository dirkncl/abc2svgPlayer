%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% 
% This file is a part of the Open Hymnal Project to create a freely 
% distributable, downloadable database of Christian hymns, spiritual 
% songs, and prelude/postlude music.  This music is to be distributed 
% as complete scores (words and music), using all accompaniment parts, 
% in formats that are easily accessible on most computer OS's and which
% can be freely modified by anyone.  The current format of choice is the 
% "ABC Plus" format, favored by folk music distributors on the internet.
% All scores will also be converted into pdf, MIDI, and mp3 formats.
% Some advanced features of ABC Plus are used, and for accurate 
% translation to a printed score, please consider using "abcm2ps" 
% version 4.10 or later.  I am doing my best to create a final product
% that is "Hymnal-quality", and could feasibly be used as the basis for
% a printed church hymnal.
%
% The maintainer of the Open Hymnal Project is Brian J. Dumont (brian 
% dot j dot dumont at gmail dot com).  I have gone through serious efforts 
% to make sure that no copyright mistakes make it into this database.
% If I am in error, please inform me as soon as possible.
%
% This entire effort has used only free software, and I am indebted to 
% the efforts of many other individuals, including the authors of
% the various ABC and ABC Plus software.  Undoubtedly, I am also indebted 
% to all of the great Christians who wrote these hymns.
%
% This database comes with no guarantees whatsoever.
%
% I would love to get email from anyone who uses the Open Hymnal, and
% I will take requests for hymns to add.  My decision of whether to 
% add a hymn will be based on these criteria (in the following order):
% 1) It must be in the public domain or freely distributable
% 2) It must be a Christian piece
% 3) Whether I have access to a printed copy of the music (surprisingly,
%    a MIDI file is usually a terrible source)
% 4) Whether I like the hymn :)
%
% If you would like to contribute to the Open Hymnal Project, please 
% send an email to me, I would love the help!  PLEASE EMAIL ME IF YOU 
% FIND ANY MISTAKES, no matter how small.  I want to ensure that every 
% slur, stem, hyphenation, and punctuation mark is correct; and I'm sure 
% that there must be mistakes right now.
%
% Open Hymnal Project, 2011 Edition
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% PAGE LAYOUT
%
%%pagewidth	21.6000cm
%%pageheight	27.9000cm
%%scale		0.750000
%%staffsep	1.600000cm
%%sysstaffsep	0.00000cm
%%exprabove	false
%%measurebox	false
%OHSCRIP Jn 2:1-11, Is 62:1-5, Rev 19:6-9, Mt 19:6
%OHTOPICS {Bride/Bridegroom}, {Christian Home/Family}, {Commitment}, {Love}, {Zeal [3]}
%OHCATEGORY CHRISTIAN HOME
%OHMETRICAL 8 6 8 6 8 6 8 6
%OHCOMPLEXITY 4
%OHCOMPOSER Westlake, Frederick (1840-1898)
%OHARRANGER unknown
%OHAUTHOR Thrupp, Adelaide (1831-1908)
%OHTRANSLATOR none
%%postscript /st { moveto -8 0 rmoveto 0 8 rlineto 4 0 rlineto stroke } def
%%postscript /nd { moveto -8 0 rmoveto 0 8 rlineto -4 0 rlineto stroke } def
%%deco sintro 6 st 6 0 0 
%%deco eintro 6 nd 6 0 0 

X: 1
T: Lord, Who at Cana's Wedding Feast
C: Words: Adelaide Thrupp, 1853. Music: 'St. Ursula' Frederick Westlake, 1863. 
C: Setting: "The Church Hymnal, Revised and Enlarged" (Episcopal), 1896.
C: copyright: public domain.  This score is a part of the Open Hymnal Project, 2011 Revision.
S: Music source: "The Church Hymnal, Revised and Enlarged" (Episcopal), 1896 Ed. Charles Hutchins Hymn 237.
S: words compared against "The Church Hymnal, Revised and Enlarged", 1896
M: 4/4 % time signature
L: 1/4 % default length
%%staves (S1V1 S1V2) | (S2V1 S2V2) 
V: S1V1 clef=treble 
%%MIDI beat 127 125 110 1
V: S1V2 
%%MIDI beat 127 125 110 1
V: S2V1 clef=bass 
%%MIDI beat 127 125 110 1
V: S2V2 
%%MIDI beat 127 125 110 1
K: G % key signature
%
%%MIDI program 1 0 % Piano 1
%%MIDI program 2 0 % Piano 1
%%MIDI program 3 0 % Piano 1
%%MIDI program 4 0 % Piano 1
%
% 1
[V: S1V1] [Q:1/4=110] B | d3/2 c/ B G | B A G D | E G G3/2 A/ | F3
w: 1.~Lord, who at Ca- na's wed- ding feast Didst as a Guest ap- pear,
w: 2.~The ho- liest vow that man can make, The gol- den thread in life,
w: 3.~On those who at Thine al- tar kneel, O Lord, Thy bless- ing pour,
[V: S1V2] G | G F G E | D D D B, | C B, D E | D3
[V: S2V1] D | B, C D C | C C B, G, | G, G, G, C | A,3
[V: S2V2] G, | G,, A,, B,, C, | D, E,/F,/ G, G,, | C, E, B,, C, | D,3
% 5
[V: S1V1] A | d3/2 c/ B e/d/ | c3/2 B/ A B | G B d3/2 ^c/ | B3
w: Thou dear- er far than * earth- ly guest, Vouch- safe Thy pre- sence here.
w: The bond that none may * dare to break, That bind- eth man and wife,
w: That each may wake the * o- ther's zeal To love Thee more and more.
[V: S1V2] F | G3/2 F/ G E | E3/2 E/ E ^D | E G F E | D3
[V: S2V1] D | D3/2 D/ D G, | A,3/2 ^G,/ A, F, | G, G,/A,/ B, ^A, | B,3
[V: S2V2] C, | B,,3/2 A,,/ G,, C,/B,,/ | A,,3/2 B,,/ C, B,, | E, E, F, F,, | B,,3
% 9
[V: S1V1] d | d3/2 d/ e d | d3/2 c/ B B | G3/2 G/ c e | A3
w: For ho- ly Thou in- deed dost prove The mar- riage vow to be,
w: Which, blest by Thee, what- e'er be- tide, No e- vil shall des- troy,
w: Oh, grant them here in peace to live, In pur- i- ty and love,
[V: S1V2] F | G3/2 G/ G G | A3/2 A/ G G | E3/2 E/ E E | D3
[V: S2V1] B, | B,3/2 B,/ C B, | A,3/2 D/ D D | C3/2 C/ C C | (D C B,)
[V: S2V2] B,/A,/ | G,3/2 G,/ G, G, | F,3/2 F,/ G, G, | C B, A, G, | (F, E, D,)
% 13
[V: S1V1] !sintro!A | d3/2 d/ e d | c d B A | G3/2 G/ G F | G3 !eintro!|]
w: Pro- claim- ing it a type of love Be- tween the Church and Thee.
w: Through care- worn days each care div- ides And dou- bles ev- ery joy.
w: And, this world leav- ing, to re- ceive A crown of life a- bove.
[V: S1V2] E/F/ | G3/2 G/ G ^G | A A G F | G3/2 E/ D D | D3 |]
[V: S2V1] A, | G,3/2 G,/ C B, | A, A, D C | B,3/2 C/ B, A, | B,3 |]
[V: S2V2] C, | B,,3/2 B,,/ C, E, | A, F, G, D, | E,3/2 C,/ D, D, | G,,3 |]
